import glob
import os
from pathlib import Path
import subprocess

cluster_path = "/home/daria/Documents/projects/ABC/clades/lists_phylofactor" #path to directory with a text file for each cluster, which contains a list of fasta paths to memebers of the cluster
out = "pangraph"
clusters = [os.path.basename(el).replace('.txt','') for el in glob.glob(f"{cluster_path}/*.txt")]
#clusters = ["st131_cl470_community_0_subcommunity_470"]


def get_list(cluster):
    files = []
    with open(f"{cluster_path}/{cluster}.txt") as f:
        for line in f:
            fasta = line.strip()
            files.append(fasta)
            genome = os.path.basename(fasta).replace('.fna','')
            fasta = Path(fasta)
            if not fasta.is_file():
                try:
                    subprocess.run(f"scp daria@codon-slurm-login.ebi.ac.uk:/nfs/research/zi/daria/game_of_clones/fastas/{genome}.fna /home/daria/Documents/projects/ABC/goc/fastas", shell=True, check=True, capture_output=True)
                except subprocess.CalledProcessError as e:
                    print(e.stderr.decode())
                    print(e)
                    raise e
    return files


rule all:
    input:
        rtt_snp = [f"fitch/scores/filtered/{cluster}_rtt_snp_scores.tsv" for cluster in clusters],
        rtt_count = [f"fitch/scores/{cluster}_rtt_scores.tsv" for cluster in clusters],
        unimogs = [f"unimogs/{cluster}.unimog" for cluster in clusters]

rule ggcaller:
    input:
        fasta_list = lambda wildcards: f"{cluster_path}/{wildcards.cluster}.txt"
    output:
        ann_dir = directory(f"ggcaller/{{cluster}}")
    conda:
        "ggc_env"
    resources:
        mem_mb=lambda wildcards, attempt: 40000*attempt
    threads: 8
    shell:
        "ggcaller --refs {input.fasta_list} --out {output.ann_dir} --repeat --threads {threads}"


rule start:
    input:
        ann_dir = f"ggcaller/{{cluster}}",
        fastas = lambda wildcards: get_list(wildcards.cluster)
    output:
        same_start = directory("fastas/{cluster}")
    params:
        cluster = lambda wildcards: wildcards.cluster
    script: "change_start.py"


rule pangraph:
    input:
        "fastas/{cluster}"
    output:
        gfa = f"{out}/{{cluster}}/pangraph.gfa",
	    json = f"{out}/{{cluster}}/pangraph.json"
    params:
        fastas = lambda wildcards: [file.replace(".fna", "_start_moved.fna").replace("/home/daria/Documents/projects/ABC/goc/fastas", f"fastas/{wildcards.cluster}") for file in get_list(wildcards.cluster)]
    #conda:
    #    "mmseqs2"
    resources:
        mem_mb=lambda wildcards, attempt: 40000*attempt
    threads: 8
    shell:
        """
        pangraph build --circular -k minimap2 -s 20 -b 5 --len 200 {params.fastas} > {output.json}
        pangraph export gfa --output {output.gfa} --minimum-length 200 {output.json}
        """

rule pangraph_to_unimog:
    input:
        pangraph = f"{out}/{{cluster}}/pangraph.json"
    output:
        unimog = "unimogs/{cluster}.unimog"
    conda:
        "fitch"
    script:
        "pangraph_to_unimog.py"

rule fitch_pangraph:
    input:
        tree = "/home/daria/Documents/projects/ABC/clades/trees/{cluster}.nw",
        pangraph = f"{out}/{{cluster}}/pangraph.json"
    output:
        ranges = "fitch/ranges/{cluster}_range.tsv",
        rtt = "fitch/scores/{cluster}_rtt_scores.tsv",
        relabelled_tree = "fitch/relabelled_trees/{cluster}.nw"
    params:
        mode = "pangraph",
        cluster = lambda wildcards: wildcards.cluster
    conda:
        "fitch"
    script:
        "fitch.py"

rule fitch_snp:
    input:
        tree = "/home/daria/Documents/projects/ABC/clades/trees/{cluster}.nw",
        pangraph = f"{out}/{{cluster}}/pangraph.json"
    output:
        rtt = "fitch/scores/filtered/{cluster}_rtt_snp_scores.tsv"
    params:
        mode = "snp",
        cluster = lambda wildcards: wildcards.cluster
    conda:
        "fitch"
    script:
        "fitch.py"